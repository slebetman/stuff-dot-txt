#! /usr/bin/env tclsh

package require Tk

set TESTING 1
set TERMINAL [expr {![catch {fconfigure stdout -mode}]}]

proc color {txt c} {
	if {$::TERMINAL || $::argv == "--color"} {
		puts -nonewline "\033\[3${c}m$txt\033\[0m"
	} else {
		puts -nonewline $txt
	}
}

proc test {description script} {
	if {[catch $script]} {
		puts "[color FAIL 1]: $description\n[regsub -lineanchor -all ^ $::errorInfo \t]"
	} else {
		puts "[color PASS 2]: $description"
	}
}

proc assert {r op e} {
	if "\$r $op \$e" return
	
	foreach var {e r} {
		set $var [string map {\n \\n \t \\t} [set $var]]
	}
	error "Expected: \"$e\", Result: \"$r\""
}

proc stringReadChan {cmd args} {
	global txt
	switch -- $cmd {
		read {
			set len [lindex $args 1]
			set ret [string range $txt 0 $len-1]
			set txt [string range $txt $len end]
			return $ret
		}
		initialize {
			return {initialize finalize watch read}
		}
	}
}

proc stringWriteChan {cmd args} {
	global txt
	switch -- $cmd {
		write {
			set data [lindex $args 1]
			append txt $data
			return [string length $data]
		}
		initialize {
			return {initialize finalize watch write}
		}
	}
}

test "Parsing code" {
	uplevel #0 {source stuff}
}

set stuff {
	{0 {{Title of stuff.txt file}}}
	{1 {{Hello World} {this is a note}}}
	{2 {{Second Note} {this is another note}}}
}

test "Make sure drawStuff draws correct list" {
	drawStuff
	set expected "Hello World\nSecond Note\n\n"
	set result [$::listbox get 0.0 end]
	assert $result == $expected
}

test "Make sure drawStuff draw selected notes correctly" {
	drawStuff {0 2}
	set expected "Second Note\n\n"
	set result [$::listbox get 0.0 end]
	assert $result == $expected
}

test "Make sure readNote draws correct note" {
	readNote 2
	set expected "Second Note\nthis is another note\n"
	set result [$::notebox get 0.0 end]
	assert $result == $expected
}

test "Make sure readNote sets idx" {
	readNote 1
	assert $::idx == 1
}

set testFileContent {
Test Stuff File
________________
First Note
bla

________________

Second Note

bla bla
_____________
Third Note

bla bla bla
__________________
}

test "Make sure parseStuff works as expected" {
	set ::txt $::testFileContent
	set dummyFile [chan create read stringReadChan]	
	parseStuff $dummyFile
	chan close $dummyFile
	
	set expected {
		{0 {{Test Stuff File}}}
		{1 {{First Note} bla {}}}
		{2 {{Second Note} {} {bla bla}}}
		{3 {{Third Note} {} {bla bla bla}}}
	}
	assert [list {*}$::stuff] == [list {*}$expected]
}

test "drawStuff should work with output of parseStuff" {
	drawStuff
	set expected "First Note\nSecond Note\nThird Note\n\n"
	set result [$::listbox get 0.0 end]
	assert $result == $expected
}

test "readNote should work with output of parseStuff" {
	readNote 2
	set expected "Second Note\n\nbla bla\n"
	set result [$::notebox get 0.0 end]
	assert $result == $expected
}

test "Make sure serializeStuff works" {
	global txt
	set txt ""
	set dummyFile [chan create write stringWriteChan]
	serializeStuff $dummyFile
	chan close $dummyFile
	set expected [join [list \
		[join {{Test Stuff File}} \n] \
		[join {{First Note} bla {}} \n] \
		[join {{Second Note} {} {bla bla}} \n] \
		[join {{Third Note} {} {bla bla bla}} \n] \
	] "\n[string repeat _ 80]\n\n"]
	assert $txt == "$expected\n[string repeat _ 80]\n\n"
}

test "Make sure newNote works" {
	readNote 2
	newNote
	assert $::idx == none
	
	set result [$::notebox get 0.0 end]
	assert [string trim $result] == ""
}

test "Make sure serializeStuff saves modified notes" {
	global txt
	
	readNote 1
	$::notebox insert end mango
	
	set txt ""
	set dummyFile [chan create write stringWriteChan]
	serializeStuff $dummyFile
	chan close $dummyFile
	set expected [join [list \
		[join {{Test Stuff File}} \n] \
		[join {{First Note} bla mango} \n] \
		[join {{Second Note} {} {bla bla}} \n] \
		[join {{Third Note} {} {bla bla bla}} \n] \
	] "\n[string repeat _ 80]\n\n"]
	assert $txt == "$expected\n[string repeat _ 80]\n\n"
}

test "Make sure serializeStuff saves new note" {
	global stuff txt
	
	newNote
	update
	$::notebox insert end mango
	update
	
	set txt ""
	set dummyFile [chan create write stringWriteChan]
	serializeStuff $dummyFile
	chan close $dummyFile
	set expected [join [list \
		[join {{Test Stuff File}} \n] \
		[join {{First Note} bla mango} \n] \
		[join {{Second Note} {} {bla bla}} \n] \
		[join {{Third Note} {} {bla bla bla}} \n] \
		[join {{mango}} \n]
	] "\n[string repeat _ 80]\n\n"]
	assert $txt == "$expected\n[string repeat _ 80]\n\n"
	assert [lindex $stuff end 1] == "mango"
}

test "Make sure deleteNote works" {
	global stuff idx

	readNote 4
	deleteNote
	assert [string trim [$::notebox get 0.0 end]] == ""
	assert $::idx == "none"
	assert [lindex $stuff 4 0] == "deleted"
}

test "Make sure deleteNote redraw notes list correctly" {
	set ::txt $::testFileContent
	set dummyFile [chan create read stringReadChan]	
	parseStuff $dummyFile
	chan close $dummyFile
	
	readNote 2
	deleteNote
	
	set expected "First Note\nThird Note\n\n"
	set result [$::listbox get 0.0 end]
	assert $result == $expected
}

test "Make sure deleted notes are not serialized" {
	set ::txt $::testFileContent
	set dummyFile [chan create read stringReadChan]	
	parseStuff $dummyFile
	chan close $dummyFile
	
	readNote 2
	deleteNote
	
	set ::txt ""
	set dummyFile [chan create write stringWriteChan]
	serializeStuff $dummyFile
	chan close $dummyFile
	
	set expected [join [list \
		[join {{Test Stuff File}} \n] \
		[join {{First Note} bla {}} \n] \
		[join {{Third Note} {} {bla bla bla}} \n] \
	] "\n[string repeat _ 80]\n\n"]
	assert $::txt == "$expected\n[string repeat _ 80]\n\n"
}

exit
